REPOS    QSTART 'QUEUE COMMAND - DATASET REPOSITIONING ROUTINES'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   BRANCH TO PROPER ROUTINE                                          *
*                                                                     *
***********************************************************************
         CLC   QCODEH,=H'32'  NEED TO CHECK FOR VALID DATA SET?   GP@P6
         BH    GO             NO, SO PROCEED                      GP@P6
         SPACE 1                                                  UF003
CHKID    CLC   QPDSID,=H'0'   IS THERE A VALID DATASET?
         BNE   GO             YES. GO AHEAD.
         QTILT '*** YOU ARE NOT PROCESSING A VALID DATASET ***'
GO       LH    R1,QCODEH      LOAD FUNCTION CODE
         CH    R1,=H'44'      IS THE FUNCTION SUPPORTED?
         BH    STOP           NO. RETURN.
         B     *+4(R1)        BRANCH TO ROUTINE
         B     TILT           0 OFFSET
         B     FIND           4
         B     FINDTIME       8
         B     COLUMN         12
         B     AT             16
         B     PLUS           20
         B     MINUS          24
         B     TOP            28
         B     BOTTOM         32
         B     MODEL          36                            GP@P6 UF003
         B     MONO           40                                  GP@P6
         B     COLOR          44                                  GP@P6
TILT     QTILT '*** PARAMETER IS INVALID OR OMITTED ***'
***********************************************************************
*                                                                     *
*   REPOSITION VERTICALLY                                             *
*                                                                     *
***********************************************************************
* SKIP FORWARD
PLUS     CLI   QPARM1,C' '    DEFAULT NEEDED
         BNE   PLUS010        NO
         MVI   QLNG1+1,X'01'  YES, SET LENGTH OF PARM
         MVI   QPARM1,X'F1'   SET DEFAULT TO 1
PLUS010  CLI   QPARM1,C'M'    FORWARD MAXIMUM?                   GP/MAH
         BE    BOTTOM         -> YES - PRETEND COMMAND WAS 'BOTTOM' MAH
         BAL   R4,NUMERIC     VALIDATE PARAMETER
         AP    QPREC,COUNT    ADD COUNT TO CURRENT RECORD
         B     RESUME         CALL LISTDS
* SKIP BACKWARD
MINUS    CLI   QPARM1,C' '    DEFAULT NEEDED
         BNE   MINUS010       NO
         MVI   QLNG1+1,X'01'  YES, SET LENGTH OF PARM
         MVI   QPARM1,X'F1'   SET DEFAULT TO 1
MINUS010 CLI   QPARM1,C'M'    BACK MAXIMUM?                      GP/MAH
         BE    TOP            -> YES - PRETEND COMMAND WAS 'TOP' GP/MAH
         BAL   R4,NUMERIC     VALIDATE PARAMETER
         SP    QPREC,COUNT    SUBTRACT COUNT FROM CURRENT RECORD
         BP    RESUME         CALL LISTDS IF RESULT POSITIVE.
* TOP OF DATASET
TOP      ZAP   QPREC,=P'1'    SET CURRENT RECORD TO TOP OF DS
* RESUME AT CURRENT RECORD
RESUME   L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
STOP     QSTOP
* BOTTOM OF DATASET
BOTTOM   ZAP   QPREC,=P'9999999' SET CURRENT RECORD TO LARGEST
         MVI   QCODE,32       TELL LISTDS DOWN MAX = BOTTOM       GP@P6
         B     RESUME         CALL LISTDS
* POSITION TO THIS RECORD
AT       BAL   R4,NUMERIC     VALIDATE PARAMETER
         ZAP   QPREC,COUNT    INDICATE REPOSITION NO
         B     RESUME         CALL LISTDS
* CHECK THE PARMETER FOR VALID NUMERIC AND PACK IT
NUMERIC  LH    R2,QLNG1       LENGTH OF PARM
         SH    R2,=H'1'       IS THE COUNT FIELD ZERO?
         BM    RESUME         YES. RESUME FROM CURRENT POSITION.
         CLI   QPARM1,C'P'    FULL PAGE SPACING?                 GP/MAH
         BE    FOUNDPG        -> YES - MULIPLY BY PAGESIZE       GP/MAH
         CLI   QPARM1,C'H'    HALF PAGE SPACING?                 GP/MAH
         BE    FOUNDHF        -> YES - MULIPLY BY HALFPAGESIZE   GP/MAH
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC CHECK
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R2,PACK        PACK THE FIELD INTO COUNT
         CLI   QSUBNAME,C'P'  ?/PAGE FORWARD/BACKWARD SPECIFIED
         BE    FOUNDP         YES. GO PROCESS
         CLI   QSUBNAME,C'H'  ?/HALF PAGE FORWARD/BACKWARD SPECIFIED
         BNE   NEXIT          NO. GO TO EXIT
         MP    COUNT,QDLNES   YES. MULTIPLY BY PAGE SIZE          UF003
FOUNDH   DP    COUNT,=P'2'    AND CONVERT TO HALF PAGES           UF003
         ZAP   COUNT,COUNT(7) RE-ALIGN THE STUPID FIELD           UF003
         B     NEXIT
FOUNDHF  ZAP   COUNT,QDLNES   SET SCROLL AMOUNT TO PAGE SIZE      GP@P6
         B     FOUNDH         NOW GO HALVE IT                     GP@P6
FOUNDP   MP    COUNT,QDLNES   YES. MULTIPLY BY PAGE SIZE          UF003
         B     NEXIT                                              GP@P6
FOUNDPG  ZAP   COUNT,QDLNES   SET SCROLL AMOUNT TO PAGE SIZE      GP@P6
NEXIT    BR    R4             RETURN
* EXECUTED INSTRUCTIONS
MVZ      MVZ   QFZONES(1),QPARM1 CHECK FOR NUMERIC
PACK     PACK  COUNT,QPARM1(1) PACK COUNT
***********************************************************************
*                                                                     *
*   REPOSITION HORIZONTALLY                                           *
*                                                                     *
***********************************************************************
COLUMN   CLI   QPARM1,C' '    ?/DEFAULT NEEDED
         BE    COL010         YES, GO SET IT
         CLI   QPARM1,C'0'    ?/SET COLUMN TO 1
         BNE   COL020         NO, TEST IF NUMERIC
COL010   MVI   QLNG1+1,X'01'  SET LENGTH OF PARM
         MVI   QPARM1,X'F1'   SET COLUMN TO 1
COL020   BAL   R4,NUMERIC     VALIDATE PARAMETER
         CP    COUNT,=P'255'  IS THE COUNT FIELD TOO BIG?
         BH    TILT           YES. TILT.
         SP    COUNT,=P'1'    COLUMN RELATIVE TO ZERO
         BM    TILT           INVALID. TILT.
         CVB   R5,COUNT       CONVERT TO BINARY
         STH   R5,QPOFFSET    STORE IN QPOFFSET
         B     RESUME         CALL LISTDS
***********************************************************************
*                                                                     *
*   LOCATE SPECIFIC RECORD                                            *
*                                                                     *
***********************************************************************
FIND     CLI   QSUBNAME+1,C'A' IS THE REQUEST FOR FIND ALL?
         BE    FIND2          YES. DO NOT UPDATE RECORD POINTER.
         AP    QPREC,=P'1'    START SEARCH AT NEXT RECORD
FIND2    CLI   QPARM1,C' '    IS THERE A PARAMETER?
         BE    RESUME         NO. CONTINUE WITH PREVIOUS FIND.
         LA    R2,QDREPLY+62  END OF USER REPLY
         LA    R3,61          MAXIMUM LENGTH OF PARM
         SH    R3,QOFF1       OFFSET TO FIRST PARM
LOOP     CLC   0(1,R2),QPARM1 IS THIS THE DELIMITER?
         BE    FOUND          YES. CONTINUE.
         BCTR  R2,0           TRY THE PREVIOUS BYTE
         BCT   R3,LOOP        IS THE LENGTH FIELD EXHAUSTED?
         B     TILT           YES. TILT.
FOUND    BCTR  R3,0           DECREMENT LENGTH BY 1
         LA    R2,QDREPLY+1   ADDR OF REPLY + 1
         AH    R2,QOFF1       ADDR OF FIRST BYTE OF PARM
         EX    R3,MVC         MOVE PARM TO QPFIND
         STH   R3,QPLNG       SAVE LENGTH-1 OF PARM
         B     COLTEST        TEST IF COLUMN KEYWORD IS PRESENT
MVC      MVC   QPFIND(1),0(R2) MOVE PARM TO QPFIND
***********************************************************************
*                                                                     *
*    COLUMN RANGE FOR FIND                                            *
*                                                                     *
***********************************************************************
COLTEST  XC    QOFFS,QOFFS    RESET COLUMN OFFSETS
         XC    QOFFE,QOFFE
         LA    R2,3(R2,R3)    PREPARE FOR NEXT FIELD
         LA    R3,QDREPLY+55  END OF USER REPLY
LOOP1    CLC   0(3,R2),=CL3'COL'  ?/COLUMN PARAMETER
         BE    FOUNDA         YES. GO PROCESS
         LA    R2,1(R2)       NO. LOOK AT NEXT FIELD
         CR    R2,R3          ?/END OF REPLY
         BH    RESUME         YES. EXIT
         B     LOOP1          NO. CHECK AGAIN
FOUNDA   LA    R2,3(R2)       NEXT FIELD
         CLI   0(R2),C'('     ?/LP PRESENT
         BNE   PRESUME        NO. EXIT
         LA    R2,1(R2)       NEXT FIELD
         LA    R3,QDREPLY+62  END OF REPLY
         LR    R5,R2          SAVE START OF THIS FIELD
         MVI   QDELIMIT,C','  LOOKING FOR DELIMETER = ','
         BAL   R7,CHKDEL      GO CHECK THE DELIMETER
         CVB   R6,COUNT       CONVERT START COLUMN OFFSET TO BINARY
         LTR   R6,R6          ?/USER SPECIFY OFFSET OF ZERO
         BZ    PRESUME        YES. GO INDICATE AN ERROR
         BCTR  R6,0           DECREMENT OFFSET BY 1
         STH   R6,QOFFS       SAVE START COLUMN
         LA    R2,1(R2)       NEXT FIELD
         LR    R5,R2          SAVE START OF THIS FIELD
         MVI   QDELIMIT,C')'  LOOKING FOR DELIMETER = ')'
         BAL   R7,CHKDEL      GO CHECK THE DELIMETER
         CVB   R6,COUNT       CONVERT END COLUMN OFFSET TO BINARY
         LTR   R6,R6          ?/USER SPECIFY OFFSET OF ZERO
         BZ    PRESUME        YES. GO INDICATE AN ERROR
         BCTR  R6,0           DECREMENT OFFSET BY 1
         STH   R6,QOFFE       SAVE END COLUMN
         CLC   QOFFS,QOFFE    ?/ERROR IN COLUMN SPECIFICATION
         BNL   PRESUME        YES. DISREGARD COLUMN SEARCH
         B     RESUME         NO. CONTINUE WITH NORMAL PROCESSING
CHKDEL   CLC   0(1,R2),QDELIMIT ?/DELIMITER FOUND
         BE    CHK010         YES. GO PROCESS
         LA    R2,1(R2)       NO. LOOK AT NEXT FIELD
         CR    R2,R3          ?/END OF REPLY
         BH    PRESUME        YES. EXIT
         B     CHKDEL         NO. CHECK AGAIN
CHK010   LR    R4,R2          SAVE DELIMITER ADDRESS
         SR    R4,R5          LENGTH OF SUB-PARM
         CH    R4,=H'3'       ?/LENGTH TO LONG
         BNL   PRESUME        YES. DISREGARD COLUMN SEARCH
         BCTR  R4,0           LENGTH FOR EXEC INTRUCTION
         MVC   QFZONES,QFZONE  NUMERIC TEST
         EX    R4,MVZ1
         CLC   QFZONES,QFZONE ?/FIELD NUMERIC
         BNE   TILT           NO. EXIT WITH ERROR MESSAGE
         EX    R4,PACK1
         BR    R7
PRESUME  XC    QOFFS,QOFFS    DISREGARD FIND
         XC    QOFFE,QOFFE       BY COLUMN RANGE
         QTILT ' *** ERROR IN COLUMN SPECIFICATION ***'
MVZ1     MVZ   QFZONES(1),0(R5)
PACK1    PACK  COUNT,0(1,R5)
***********************************************************************
*                                                                     *
*   REPOSITION IN SYSLOG DATASET BY TIME OF DAY                       *
*                                                                     *
***********************************************************************
FINDTIME CLI   QPARM1,C' '    IS THE PARM OMITTED?
         BE    TILT           YES. TILT.
         B     RESUME         CALL LISTDS
         EJECT                                                    UF003
******************************************************************UF003
*                                                                 UF003
*   SWITCH MODEL TYPE FOR 3270 TERMINAL                           UF003
*                                                                 UF003
******************************************************************UF003
MODEL    DS    0H             ,                                   UF003
         GTTERM PRMSZE=QDPRMSZ,ALTSZE=QDALTSZ,ATTRIB=QDATTRB,     GP@P6+
               TERMID=QDTRMID,MF=(E,QDGTRM)  HANDLE RECONNECT     GP@P6
         CLC   QLNG1,=H'1'    SINGLE CHARACTER OPERAND?           GP@P6
         BNE   MODELBAD       NO, UNSUPPORTED MODEL               GP@P6
         CLI   QPARM1,C'2'    MODEL 2?                      GP@P6 UF003
         BE    MODEL2         YES, BRANCH                         UF003
         CLI   QPARM1,C'3'    MODEL 3?                      GP@P6 UF003
         BE    MODEL3         YES, BRANCH                         UF003
         CLI   QPARM1,C'4'    MODEL 4?                      GP@P6 UF003
         BE    MODEL4         YES, BRANCH                         UF003
         CLI   QPARM1,C'5'    MODEL 5?                      GP@P6 UF003
         BE    MODEL5         YES, BRANCH                         UF003
         CLI   QPARM1,C'X'    MODEL X?                            GP@P6
         BE    MODELX         YES, BRANCH                         GP@P6
MODELBAD QTILT 'UNSUPPORTED MODEL TYPE'                           UF003
         SPACE 1                                                  UF003
*              REDO MODEL CODE                       2002-06-16   GP@P6
*              REMOVE ALL STSIZE MACROS              2003-02-05   GP@P6
MODEL2   MVI   QDSCRO1,X'F5'       ERASE/WRITE                        *
****     STSIZE SIZE=80,LINE=24    SET SCREEN SIZE FOR VTAM           *
         LA    R1,80               SCREEN WIDTH                       *
         LA    R0,24               SCREEN DEPTH                       *
         B     MODELN              JOIN COMMON CODE                   *
MODEL3   MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE              *
****     STSIZE SIZE=80,LINE=32    SET SCREEN SIZE FOR VTAM           *
         LA    R1,80               SCREEN WIDTH                       *
         LA    R0,32               SCREEN DEPTH                       *
         B     MODELN              JOIN COMMON CODE                   *
MODEL4   MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE              *
****     STSIZE SIZE=80,LINE=43    SET SCREEN SIZE FOR VTAM           *
         LA    R1,80               SCREEN WIDTH                       *
         LA    R0,43               SCREEN DEPTH                       *
         B     MODELN              JOIN COMMON CODE                   *
MODEL5   MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE              *
****     STSIZE SIZE=132,LINE=27   SET SCREEN SIZE FOR VTAM           *
         LA    R1,QDSCRTXT+80                              2003-02-05 *
         LA    R1,132              SCREEN WIDTH                       *
         LA    R0,27               SCREEN DEPTH                       *
         B     MODELN              JOIN COMMON CODE                   *
MODELX   MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE   2003-02-14 *
         SLR   R0,R0                                       2003-02-14 *
         IC    R0,QDALTSZ          GET ALTERNATE LINES     2003-02-14 *
         SLR   R1,R1                                       2003-02-14 *
         IC    R1,QDALTSZ+1        GET ALTERNATE COLUMNS   2003-02-14 *
         CLC   QDALTSZ,QDPRMSZ     TWO DIFFERENT SIZES?    2003-02-14 *
         BNE   MODELN              YES                     2003-02-14 *
         MVI   QDSCRO1,X'F5'       NO, USE ERASE/WRITE     2003-02-14 *
         SPACE 1                                                      *
MODELN   STH   R1,QDLNELEN         LINE LENGTH                        *
         SH    R1,=H'80'           WIDER THAN 80-COLUMNS?  2003-02-14 *
         BNP   TOPLNOK             NO                      2003-02-14 *
         LA    R14,QDSCRTXT+80     YES, POINT TO COLUMN 81 2003-02-14 *
         LR    R15,R1              GET EXTRA WIDTH SIZE    2003-02-14 *
         SLR   R1,R1               ZERO SOURCE LENGTH      2003-02-14 *
         ICM   R1,8,QBLANK         USE BLANK PAD CHARACTER 2003-02-14 *
         MVCL  R14,R0              BLANK REST OF TOP LINE  2003-02-14 *
         LA    R1,QDSCRTXT+80      POINT TO COLUMN 81      2003-02-14 *
         MVC   2(7,R1),=X'E4A2859989847E'      C'USERID='  2003-02-14 *
         MVC   9(8,R1),QLOGON      DISPLAY USERID          2003-02-14 *
         MVC   19(9,R1),=X'E3859994899581937E' C'TERMINAL='2003-02-14 *
         MVC   28(8,R1),QDTRMID    DISPLAY TERMINAL NAME   2003-02-14 *
TOPLNOK  LR    R15,R0              GET SCREEN LINE COUNT              *
         SH    R15,=H'3'           GET DETAIL DISPLAY LINE COUNT      *
         CVD   R15,COUNT           MAKE DECIMAL                       *
         ZAP   QDLNES,COUNT        DETAIL LINE COUNT       2003-02-05 *
         MH    R0,QDLNELEN         GET SCREEN DISPLAY CHARACTER COUNT *
         ST    R0,QDSCRPLN         LENGTH OF BUFFER WITH SHADOW BUFFER*
         MH    R15,QDLNELEN        GET DISPLAY DETAIL CONTENT CAPACITY*
         ST    R15,QDSCRLEN        LENGTH TO CLEAR FOR EACH DISPLAY   *
         LR    R15,R0              GET SCREEN BUFFER SIZE             *
         SH    R15,QDLNELEN        GET CAPACITY LESS ONE SCREEN LINE  *
         LA    R1,QDSCRTXT(R15)                                       *
         MVC   0(7,R1),=C'REPLY -'                                    *
         MVI   7(R1),X'0B'         HIGH INTENSITY INPUT FIELD         *
         XC    8(63,R1),8(R1)      CLEAR INPUT AREA                   *
         MVI   71(R1),X'0C'        LOW INTENSITY OUTPUT FIELD         *
         LA    R1,72(,R1)          POINT TO PLUS SIGN INDICATOR AREA  *
         ST    R1,QDPLUS@          SAVE ITS ADDRESS                   *
         LA    R1,QDSCRTXT         POINT TO FIRST SCREEN LINE         *
         AH    R1,QDLNELEN         POINT TO SECOND SCREEN LINE        *
         ST    R1,QDHLINE@         SAVE HEADING LINE ADDRESS          *
         AH    R1,QDLNELEN         POINT TO THIRD SCREEN LINE         *
         ST    R1,QDLINE1@         SAVE FIRST DATA LINE ADDRESS       *
         L     R1,QDSHADO@         POINT TO THE SHADOW AREA           *
         XC    0(256,R1),0(R1)     CLEAR THE TITLE LINE               *
         MVI   0(R1),X'07'         TOP LEFT TEXT IN WHITE             *
         MVC   1(14,R1),0(R1)                                         *
         MVI   16(R1),X'02'        COMMAND ECHO IN RED                *
         MVI   80(R1),X'06'        YELLOW USERID+TERMINAL  2003-02-14 *
         AR    R1,R15              POINT TO SHADOW OF LAST LINE       *
         MVI   0(R1),X'07'         PROMPT IN WHITE                    *
         MVC   1(6,R1),0(R1)                                          *
         MVI   8(R1),X'32'         INPUT AREA IN RED UNDERSCORE       *
         MVC   9(62,R1),8(R1)                                         *
         MVI   72(R1),X'07'        PLUS SIGN IN WHITE IF SHOWN        *
         MVC   73(7,R1),72(R1)                                        *
         OI    QFLAG1,QFLG1RSH     ALTER SCREEN SIZE 2003-02-05       *
         CLI   QDLNELEN+1,132      132 COLUMNS?      2003-02-05       *
         BL    RESUMEIT            NO                2003-02-05       *
         MVC   80(52,R1),79(R1)    YES, FILL IN REST 2003-02-05       *
RESUMEIT DS    0H      END OF REDO MODEL CODE        2002-06-16   GP@P6
         CLC   QPDSID,=H'0'        IS THERE A DATASET?            UF003
         BNE   RESUME              YES, CALL LISTDS               UF003
         L     R15,QDHLINE@        POINT TO HEADING LINE          GP@P6
         MVC   0(80,R15),QBLANK    BLANK HEADING LINE       GP@P6 UF003
         B     STOP                AND RETURN                     UF003
         SPACE 1                                                  UF003
***********************************************************************
*                                                                     *
*   SWITCH EXTENDED DATA STREAM USAGE             GP@P6 NOVEMBER 2002 *
*                                                                     *
***********************************************************************
MONO     NI    QFLAG2,255-QFLG2EDS RESET BIT
         B     RESUMEIT
COLOR    OI    QFLAG2,QFLG2EDS     SET BIT
         B     RESUMEIT
         EJECT                                                    UF003
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QCOMMON
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
WORK     DSECT
         DS    CL72
COUNT    DS    D
         END
