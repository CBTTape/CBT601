SYSLOG   QSTART 'QUEUE COMMAND - PRINT THE SYSTEM LOG DATASET'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
***********************************************************************
*                                                                     *
*   DETERMINE CURRENT SYSLOG JOB NUMBER               GP@P6 JULY 2002 *
*                                                                     *
***********************************************************************
         ICM   R1,3,QLNG1     WAS A PARAMETER SPECIFIED?
         BZ    SLTHIS         NO, WANT THIS SYSTEMS SYSLOG
         CLC   QPARM1,=CL8'SYSLOG'
         BNE   SLTHAT         DO NOT INTERFERE WITH SPECIFIC REQUEST
SLTHIS   L     R3,16          POINT TO CVT
         USING CVTDSECT,R3
         L     R3,CVTJESCT    POINT TO JESCT
         DROP  R3
         USING JESCT,R3
         L     R3,JESSSCT     POINT TO SSCT
         DROP  R3
         USING SSCT,R3
         L     R3,SSCTSSVT    POINT TO SSVT
         DROP  R3
         USING SSVT,R3
         L     R3,$SVHAVT     POINT TO HAVT
         DROP  R3
         LTR   R3,R3          TEST IF ANY PITS
         BZ    SLTHAT         NO, GIVE UP SEARCH
         ICM   R3,15,4(R3)    POINT TO SJB OF ASID 1
         BZ    SLTHAT         SYSLOG MUST NOT BE ACTIVE
         CLC   236(8,R3),=CL8'SYSLOG'
         BNE   SLTHAT         SOMETHING HAS GONE AMISS
         MVC   QPARM1,QBLANK
         MVC   QPARM1+1(4),232(R3)
         OC    QPARM1(5),=CL5'10000'
         MVI   QLNG1+1,5      QPARM1 IS NOW 5-DIGIT INTERNAL JOB ID
SLTHAT   DS    0H
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK JOBNAME EQUAL SYSLOG                                        *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF IOAREA FOR JCT
         USING JCTSTART,R9    BASE REG FOR JCT
         CLC   JCTJNAME,=CL8'SYSLOG' IS THIS SYSLOG?
         BNE   TILT           NO. TILT.
***********************************************************************
*                                                                     *
*   DETERMINE NUMBER OF DATASETS FROM CURRENT DATASET                 *
*                                                                     *
***********************************************************************
         LH    R3,JCTPDDBK    HIGHEST DATASET ID NUMBER
         LH    R2,QLNG2       LENGTH OF BACKUP PARM
         SH    R2,=H'1'       IS THE BACKUP PARM ZERO LENGTH?
         BM    CALLLIST       YES. SKIP.
         CLI   QPARM2,C'-'    IS THERE A MINUS SIGN?
         BNE   PLUS           NO. SKIP.
         MVI   QPARM2,C'0'    CHANGE MINUS TO ZERO
PLUS     MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SR    R3,R2          BACK UP NUMBER OF DATASETS
         CH    R3,=H'101'     IS THE NUMBER LESS THAN 101?
         BL    TILT           YES. TILT.
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST STH   R3,QPDSID      STORE DATASET ID
         MVC   QPOFFSET,=H'0' PRINT OFFSET ZERO
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
TILT     QTILT '*** INVALID PARAMETER ***'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
         LTORG
         DROP  ,                   DROP ALL ADDRESSING            NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SYSLOG   CSECT ,                                                  UF023
JCT      EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $CVT   ,                                                 GP@P6
         $JESCT ,                                                 GP@P6
         $SSCT  ,                                                 GP@P6
         $SVT   ,                                                 GP@P6
         $JCT
WORK     DSECT
         DS    72C
CONVERT  DS    D
         QCOMMON
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
